plugins {
    id "org.springframework.boot" version "2.1.7.RELEASE"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "com.google.protobuf" version "0.8.10"
    id "org.jetbrains.kotlin.jvm" version "1.3.41"
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.41"
}

group = "com.ifantastic"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    main.java.srcDirs += 'src/main/java'
    main.java.srcDirs += 'src/main/generated-proto'
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    //protobuf
    implementation "io.grpc:grpc-stub:1.23.0"
    implementation "io.grpc:grpc-protobuf:1.23.0"
    implementation "io.grpc:grpc-netty:1.23.0"
    implementation "net.devh:grpc-server-spring-boot-starter:2.5.0.RELEASE"
    //fastjson json序列化
    implementation "com.alibaba:fastjson:1.2.58"
    //kotlin
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    runtimeOnly "mysql:mysql-connector-java"
    //test
    testImplementation "org.springframework.boot:spring-boot-starter-test"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.9.1"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.23.0"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    outputSubDir = 'generated-proto'
                }
            }
            task.plugins {
                grpc {
                    outputSubDir = 'generated-proto'
                }
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/src/"
}

clean {
    delete "${projectDir}/src/main/generated-proto"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}